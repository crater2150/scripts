#!/bin/zsh
#dep:tmux alacritty

if [[ "${0:t}" == "guiterm" ]]; then
	<<-HELP
	Symlink to g\$progname to start \$progname in tmux in a new terminal or
	reattach a tmux session named \$progname in a new terminal

	Symlink to g.\$progname to start \$progname in tmux in a new terminal
	regardless of existing tmux sessions"

	Example symlink names:
	    gmutt: start mutt in a terminal or open the existing one
	    g.zsh: open a terminal running a new zsh session
	HELP
	exit 1
fi

prog=${${0:t}[2,-1]}
if [[ "$prog[1]" == "." ]]; then
	prog=${prog[2,-1]}
	action="tmux new-session ${prog}"
elif tmux list-sessions | grep -qe "^${prog}:"; then
	action="tmux attach -t ${prog}"
else
	action="tmux new-session -s ${prog} ${prog}"
fi

#exec alacritty --title G${prog} -e ${=action}
exec alacritty -t G${prog} -e ${=action}
